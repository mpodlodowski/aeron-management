plugins {
    id 'application'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'org.graalvm.buildtools.native' version '0.10.4'
}

application {
    mainClass = 'it.podlodowski.aeronmgmt.agent.AgentMain'
}

dependencies {
    implementation project(':common')
    implementation 'io.aeron:aeron-all:1.46.5'
    implementation 'org.agrona:agrona:1.20.0'
    implementation 'io.grpc:grpc-netty:1.62.2'
    implementation 'ch.qos.logback:logback-classic:1.4.14'
    implementation 'org.slf4j:slf4j-api:2.0.9'
}

graalvmNative {
    metadataRepository {
        enabled = true
    }
    binaries {
        main {
            imageName = 'agent'
            mainClass = 'it.podlodowski.aeronmgmt.agent.AgentMain'
            resources.autodetect()
            buildArgs.addAll(
                '--no-fallback',
                '--enable-url-protocols=http',
                '-H:+ReportExceptionStackTraces',
            )
        }
    }
}

shadowJar {
    archiveClassifier.set('')
    mergeServiceFiles()
}

// Disable the standard jar to avoid conflicts with shadowJar (both produce the same artifact name)
jar {
    enabled = false
}

// Wire up task dependencies so distribution tasks use the shadow jar
tasks.distZip.dependsOn tasks.shadowJar
tasks.distTar.dependsOn tasks.shadowJar
tasks.startScripts.dependsOn tasks.shadowJar
tasks.startShadowScripts.dependsOn tasks.jar

// Native compilation needs the shadow JAR (since regular jar is disabled)
tasks.named('nativeCompile') {
    dependsOn tasks.named('shadowJar')
}
tasks.named('generateResourcesConfigFile') {
    dependsOn tasks.named('shadowJar')
}
